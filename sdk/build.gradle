apply plugin: 'com.android.library'
apply plugin: 'net.saliman.cobertura'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

// library version used when deploying the artifact
version = getVersionFromGit()

// Maven Group ID for the artifact
group = 'com.stormpath.sdk'

// Maven Artifact ID
def mavenArtifactId = 'stormpath-sdk-android'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        // Fix for lint error:
        // Invalid package reference in library; not included in Android: java.nio.file. Referenced from okio.Okio.
        warning 'InvalidPackage'
    }
}

def siteUrl = 'https://github.com/stormpath/stormpath-sdk-android'      // Homepage URL of the library
def gitUrl = 'https://github.com/stormpath/stormpath-sdk-android.git'   // Git repository URL

// this ensures the archives have the correct name instead of just using the module name
project.archivesBaseName = mavenArtifactId

configurations {
    javadocDeps
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'com.squareup.moshi:moshi:1.1.0'
    compile 'com.android.support:support-annotations:23.1.1'
    javadocDeps 'com.squareup.okhttp3:okhttp:3.1.2'
    javadocDeps 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    javadocDeps 'com.squareup.moshi:moshi:1.1.0'
    javadocDeps 'com.android.support:support-annotations:23.1.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.1.2'
    testCompile "org.robolectric:robolectric:3.0"
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Stormpath Android SDK'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'reisub'
                        name 'Dino Kovaƒç'
                        email 'dino.kovac@infinum.co'
                    }
                    developer {
                        id 'stormpath'
                        name 'Stormpath Java Team'
                        email 'java@stormpath.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = getProperty('bintray.user')
    key = getProperty('bintray.apikey')
    def gpgKey = getProperty('bintray.gpg.key')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = mavenArtifactId
        userOrg = 'stormpath'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        publish = true // if set to false, you need to click publish on bintray after deploy
        version {
            gpg.sign = true
            gpg.passphrase = gpgKey
        }
    }
}

/**
 * @return the tag if this commit is tagged, a unique version identifier otherwise
 */
def getVersionFromGit() {
    return 'git describe --always'.execute().text.trim()
}